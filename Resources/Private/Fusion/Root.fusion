/**
 * Root TypoScript template for the knallimall.org site
 */

namespace: Site=JoRo.KnallImAll

include: NodeTypes/*

# Basic implementation of a flexible MultiColumn element, not exposed directly but inherited by all specific MultiColumn content elements
prototype(Neos.NodeTypes:MultiColumn) < prototype(Neos.Neos:Content) {
	templatePath = 'resource://JoRo.KnallImAll/Private/Templates/NodeTypes/MultiColumn.html'
	layout = ${q(node).property('layout')}
	attributes.class = ${'row col' + q(node).property('layout')}
	columns = Neos.Fusion:Collection {
		@context.columnLayout = ${q(node).property('layout')}
		collection = ${q(node).children('[instanceof Neos.Neos:ContentCollection]')}
		itemRenderer = Neos.NodeTypes:MultiColumnItem
		itemName = 'node'
		iterationName = 'columnIteration'
	}
}

prototype(Neos.NodeTypes:MultiColumnItem) {
	attributes.class = ${'medium-' + String.split(q(node).parent().property('layout'), '-')[columnIteration.index] + ' column'}
}


page = Page {
	head {
		stylesheets.site = Neos.Fusion:Template {
			templatePath = 'resource://JoRo.KnallImAll/Private/Templates/Page/Default.html'
			sectionName = 'stylesheets'
		}

		javascripts.site = Neos.Fusion:Template {
			templatePath = 'resource://JoRo.KnallImAll/Private/Templates/Page/Default.html'
			sectionName = 'headScripts'
		}
        meta = Neos.Fusion:Template {
                templatePath = 'resource://JoRo.KnallImAll/Private/Templates/Page/Default.html'
                sectionName = 'meta'
                title = ${q(node).property('title')}
        }
	}

	body {
		templatePath = 'resource://JoRo.KnallImAll/Private/Templates/Page/Default.html'
		sectionName = 'body'
		parts {
			menu = Menu {
				templatePath = 'resource://JoRo.KnallImAll/Private/Templates/TypoScriptObjects/Menu.html'
				maximumLevels = 3
				entryLevel = 1
			    current.attributes.class = 'item current'
			    active.attributes.class = 'item active'
			    normal.attributes.class = 'item'
				attributes = Neos.Fusion:Attributes {
				    icon = ${q(node).property('iconsFoundation')}
				}
			}
			menuOffCanvas = Menu {
				templatePath = 'resource://JoRo.KnallImAll/Private/Templates/TypoScriptObjects/MenuOffCanvas.html'
				maximumLevels = 3
				entryLevel = 1
			    current.attributes.class = 'item current'
			    active.attributes.class = 'item active'
			    normal.attributes.class = 'item'
			}
			breadcrumb = Breadcrumb
		}
		// These are your content areas, you can define as many as you want, just name them and the nodePath.
		content {
			// The default content section
			teaser >
			main = PrimaryContent {
				nodePath = 'main'
			}
		}
		imageUri = Neos.Neos:ImageUri {
			asset = ${q(node).property('image')}
			maximumWidth = 2560
			maximumHeight = 1280
			@if.image = ${q(node).property('image')}
		}

		javascripts.site = Neos.Fusion:Template {
			templatePath = 'resource://JoRo.KnallImAll/Private/Templates/Page/Default.html'
			sectionName = 'bodyScripts'
		}
	}
}